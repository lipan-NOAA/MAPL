#include "MAPL_ErrLog.h"
module MAPL_ExtDataConfig
   use ESMF
   use yaFyaml
   use gFTL_StringVector
   use MAPL_KeywordEnforcerMod
   use MAPL_ExceptionHandling
   use MAPL_ExtDataFileStream
   use MAPL_ExtDataFileStreamMap
   use MAPL_ExtDataRule
   use MAPL_ExtDataRuleMap
   use MAPL_ExtDataDerived
   use MAPL_ExtDataDerivedMap
   use MAPL_ExtDataConstants
   use MAPL_ExtDataTimeSample
   use MAPL_ExtDataTimeSampleMap
   implicit none
   private

   type, public :: ExtDataConfig
      integer :: debug
      type(ExtDataRuleMap) :: rule_map
      type(ExtDataDerivedMap) :: derived_map
      type(ExtDataFileStreamMap) :: file_stream_map
      type(ExtDataTimeSampleMap) :: sample_map
      
      contains
         procedure :: get_item_type
         procedure :: get_debug_flag
         procedure :: new_ExtDataConfig_from_yaml
   end type

contains

   recursive subroutine new_ExtDataConfig_from_yaml(ext_config,config_file,current_time,unusable,rc) 
      class(ExtDataConfig), intent(inout), target :: ext_config
      character(len=*), intent(in) :: config_file
      type(ESMF_Time), intent(in) :: current_time
      class(KeywordEnforcer), optional, intent(in) :: unusable
      integer, optional, intent(out) :: rc

      type(Parser)              :: p
      type(Configuration) :: config, subcfg, ds_config, rule_config, derived_config, sample_config
      type(ConfigurationIterator) :: iter
      character(:), pointer :: key
      type(ExtDataFileStream) :: ds
      type(ExtDataDerived) :: derived
      type(ExtDataRule) :: rule,ucomp,vcomp
      type(ExtDataTimeSample) :: ts
      integer :: status, semi_pos
      character(len=:), allocatable :: uname,vname
      type(FileStream) :: fstream
      type(StringVector) :: subconfigs
      integer :: i
      character(len=:), allocatable :: rule_key

      _UNUSED_DUMMY(unusable)

      p = Parser('core')
      fstream=FileStream(config_file)
      config = p%load(fstream)
      call fstream%close()

      subconfigs = config%at("subconfigs")
      do i=1,subconfigs%size()
         call new_ExtDataConfig_from_yaml(ext_config,subconfigs%at(i),current_time,rc=status)
         _VERIFY(status)
      enddo

      ds_config = config%at("Collections")
      rule_config = config%at("Exports")
      derived_config = config%at("Derived")
      sample_config = config%at("Samplings")

      iter = sample_config%begin()
      do while (iter /= sample_config%end())
         key => iter%key()
         subcfg = iter%value()
         call ts%set_defaults(rc=status)
         call ts%append_from_yaml(subcfg,rc=status)
         _VERIFY(status)
         call ext_config%sample_map%insert(trim(key),ts)
         call iter%next()
      enddo

      iter = ds_config%begin()
      do while (iter /= ds_config%end())
         key => iter%key()
         subcfg = iter%value()
         call ds%fill_from_yaml(subcfg,current_time,rc=status)
         _VERIFY(status)
         call ext_config%file_stream_map%insert(trim(key),ds)
         call iter%next()
      enddo

      iter = rule_config%begin()
      do while (iter /= rule_config%end())
         call rule%set_defaults(rc=status)
         _VERIFY(status)
         key => iter%key()
         subcfg=iter%value()
         call rule%append_from_yaml(subcfg,rc=status)
         _VERIFY(status)
         semi_pos = index(key,";")
         if (semi_pos > 0) then
            call rule%split_vector(key,ucomp,vcomp,rc=status)
            uname = key(1:semi_pos-1)
            vname = key(semi_pos+1:len_trim(key))
            call ext_config%rule_map%insert(trim(uname),ucomp)
            call ext_config%rule_map%insert(trim(vname),vcomp)
         else
            call ext_config%rule_map%insert(trim(key),rule)
         end if
         call iter%next()
      enddo

      if (.not.derived_config%is_none()) then
         iter = derived_config%begin()
         do while (iter /= derived_config%end())
            call derived%set_defaults(rc=status)
            _VERIFY(status)
            key => iter%key()
            rule_key="derived%"//key
             call derived%append_from_yaml(config,rule_key,rc=status) 
            _VERIFY(status)
            call ext_config%derived_map%insert(trim(key),derived)
            call iter%next()
         enddo
      end if

      call config%get(ext_config%debug,"debug",default=0,rc=status)
      _VERIFY(status)

      _RETURN(_SUCCESS)
   end subroutine new_ExtDataConfig_from_yaml

   function get_item_type(this,item_name,unusable,rc) result(item_type)
      class(ExtDataConfig), intent(inout) :: this
      character(len=*), intent(in) :: item_name
      class(KeywordEnforcer), optional, intent(in) :: unusable
      integer, optional, intent(out) :: rc
      integer :: item_type
      type(ExtDataRule), pointer :: rule
      type(ExtDataDerived), pointer :: derived

      _UNUSED_DUMMY(unusable)
      item_type=ExtData_not_found
      rule => this%rule_map%at(trim(item_name))
      if (associated(rule)) then
         if (allocated(rule%vector_component)) then
            if (rule%vector_component=='EW') then
               item_type=Primary_Type_Vector_comp2
            else if (rule%vector_component=='NS') then
               item_type=Primary_Type_Vector_comp1
            end if
         else
            item_type=Primary_Type_scalar
         end if
      end if
      derived => this%derived_map%at(trim(item_name))
      if (associated(derived)) then
         item_type=derived_type
      end if
      _RETURN(_SUCCESS)
   end function get_item_type
 
   integer function get_debug_flag(this)
      class(ExtDataConfig), intent(inout) :: this
      get_debug_flag=this%debug
   end function get_debug_flag 

end module MAPL_ExtDataConfig
