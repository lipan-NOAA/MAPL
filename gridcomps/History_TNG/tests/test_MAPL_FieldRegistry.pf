#include "MAPL_Generic.h"
#include "NUOPC_ErrLog.h"

module Test_MAPL_FieldRegistry
   use, intrinsic :: iso_fortran_env, only: INT64
   use pFUnit
   use ESMF
   use NUOPC
   use MAPL_ExceptionHandling
   use MAPL_KeywordEnforcerMod

   use MAPL_FieldRegistryEntry
   use MAPL_FieldRegistry

   character(*), parameter :: short_name       = 'short_name'
   character(*), parameter :: other_name       = 'other_name'
   character(*), parameter :: component_name   = 'component_name'
   character(*), parameter :: entry_name       = 'short_name.component_name'
   character(*), parameter :: other_entry_name = 'other_name.component_name'

   ! HACK for testing if advertise is called
   integer :: field_entry_1_call_advertise
   integer :: field_entry_2_call_advertise

   type, extends(FieldRegistryEntry) :: test_FieldRegistryEntry
   contains
      procedure :: initialize => init
      procedure :: advertise
   end type test_FieldRegistryEntry

contains
   subroutine init(this, short_name, component_name, unusable, units, alias_name)
      class(test_FieldRegistryEntry),   intent(  out) :: this
      character(*),                     intent(in   ) :: short_name
      character(*),                     intent(in   ) :: component_name
      class(KeywordEnforcer), optional, intent(in   ) :: unusable
      character(*),           optional, intent(in   ) :: units
      character(*),           optional, intent(in   ) :: alias_name

      _UNUSED_DUMMY(unusable)

      call this%FieldRegistryEntry%initialize(short_name, component_name, &
         units=units, alias_name=alias_name)

      if (short_name == other_name) then
         field_entry_2_call_advertise = 0
      else
         field_entry_1_call_advertise = 0
      end if
   end subroutine init

   subroutine advertise(this, state, unusable,&
         TransferOfferGeomObject, SharePolicyField, SharePolicyGeomObject, rc)
      class(test_FieldRegistryEntry),   intent(inout) :: this
      type(ESMF_State),                 intent(inout) :: state
      class(KeywordEnforcer), optional, intent(in   ) :: unusable
      character(*),           optional, intent(in   ) :: TransferOfferGeomObject
      character(*),           optional, intent(in   ) :: SharePolicyField
      character(*),           optional, intent(in   ) :: SharePolicyGeomObject
      integer,                optional, intent(  out) :: rc

      integer :: status

      _UNUSED_DUMMY(state)
      _UNUSED_DUMMY(unusable)
      _UNUSED_DUMMY(TransferOfferGeomObject)
      _UNUSED_DUMMY(SharePolicyField)
      _UNUSED_DUMMY(SharePolicyGeomObject)

      if (this%name() == entry_name) then
         field_entry_1_call_advertise = field_entry_1_call_advertise + 1
      else if (this%name() == other_entry_name) then
         field_entry_2_call_advertise = field_entry_2_call_advertise + 1
      end if

      _RETURN(_SUCCESS)
   end subroutine advertise
   @test
   subroutine test_map()
      type(FieldRegistry)               :: field_registry
      type(FieldRegistryEntry)          :: field_entry
      type(FieldRegistryEntry), pointer :: field_registry_entry

      call field_entry%initialize(short_name, component_name)

      @assert_that(field_registry%size(),            is(equal_to(0_INT64)))
      @assert_that(field_registry%count(entry_name), is(equal_to(0_INT64)))
      call field_registry%insert(field_entry)
      @assert_that(field_registry%size(),            is(equal_to(1_INT64)))
      @assert_that(field_registry%count(entry_name), is(equal_to(1_INT64)))

      field_registry_entry = field_registry%at(entry_name)
      @assert_that(field_registry_entry%get_short_name()     == short_name,     is(true()))
      @assert_that(field_registry_entry%get_component_name() == component_name, is(true()))
   end subroutine test_map

   @test
   subroutine test_advertise()
      type(FieldRegistry)           :: field_registry
      type(test_FieldRegistryEntry) :: field_entry_1
      type(test_FieldRegistryEntry) :: field_entry_2
      type(ESMF_State)              :: state

      integer :: status

      call field_entry_1%initialize(short_name, component_name)
      call field_entry_2%initialize(other_name, component_name)
      call field_registry%insert(field_entry_1)
      call field_registry%insert(field_entry_2)
      @assert_that(field_registry%size(), is(equal_to(2_INT64)))

      @assert_that(field_entry_1_call_advertise, is(equal_to(0)))
      @assert_that(field_entry_2_call_advertise, is(equal_to(0)))
      call field_registry%advertise(state, rc=status)
      @assert_that(status, is(equal_to(0)))
      @assert_that(field_entry_1_call_advertise, is(equal_to(1)))
      @assert_that(field_entry_2_call_advertise, is(equal_to(1)))
   end subroutine test_advertise
end module Test_MAPL_FieldRegistry
