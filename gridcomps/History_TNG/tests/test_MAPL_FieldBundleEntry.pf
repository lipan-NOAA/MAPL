#include "MAPL_Generic.h"
#include "NUOPC_ErrLog.h"

module Test_MAPL_FieldBundleEntry
   use, intrinsic :: iso_fortran_env, only: INT64
   use pFUnit
   use ESMF
   use NUOPC
   use MAPL_ExceptionHandling

   use MAPL_FieldRegistryEntry
   use MAPL_FieldBundleEntry

   character(*), parameter :: short_name     = 'short_name'
   character(*), parameter :: component_name = 'component_name'
   character(*), parameter :: units          = 'units'
   character(*), parameter :: alias_name     = 'alias_name'
   character(*), parameter :: fail_name      = 'fail_name'
   character(*), parameter :: std_out_name   = 'std_out_name'
   character(*), parameter :: alias_out_name = 'alias_name.std_out_name'

   type, extends(FieldBundleEntry) :: test_FieldBundleEntry
      integer :: call_standard_name = 0
   contains
      procedure :: standard_name
   end type test_FieldBundleEntry
contains
   function standard_name(this) result(std_name)
      character(:), allocatable :: std_name
      class(test_FieldBundleEntry), intent(inout) :: this

      this%call_standard_name = this%call_standard_name + 1

      std_name = std_out_name
   end function standard_name

   @test
   subroutine test_initialize()
      type(FieldBundleEntry) :: field_entry_no_alias
      type(FieldBundleEntry) :: field_entry_alias

      call field_entry_no_alias%initialize(short_name, component_name)
      @assert_that(field_entry_no_alias%get_short_name()     == short_name,     is(true()))
      @assert_that(field_entry_no_alias%get_component_name() == component_name, is(true()))
      @assert_that(field_entry_no_alias%get_alias_name()     == default_alias,  is(true()))

      call field_entry_alias%initialize(short_name, component_name, units=units, alias_name=alias_name)
      @assert_that(field_entry_alias%get_short_name()     == short_name,     is(true()))
      @assert_that(field_entry_alias%get_component_name() == component_name, is(true()))
      @assert_that(field_entry_alias%get_alias_name()     == alias_name,     is(true()))
   end subroutine test_initialize

   @test
   subroutine test_set_alias_name()
      type(FieldBundleEntry) :: field_entry1
      type(FieldBundleEntry) :: field_entry2

      integer :: status

      @assert_that(alias_name == '',         is(false()))
      @assert_that(fail_name  == alias_name, is(false()))

      call field_entry1%initialize(short_name, component_name)
      @assert_that(field_entry1%get_alias_name() == default_alias, is(true()))

      call field_entry1%set_alias_name(alias_name, rc=status)
      @assert_that(status, is(equal_to(0)))
      @assert_that(field_entry1%get_alias_name() == alias_name, is(true()))

      call field_entry1%set_alias_name(fail_name, rc=status)
      @assert_that(status, is(equal_to(1)))
      @assert_that(field_entry1%get_alias_name() == alias_name, is(true()))

      @assert_that(alias_name == '',         is(false()))
      @assert_that(fail_name  == alias_name, is(false()))

      call field_entry2%initialize(short_name, component_name)
      @assert_that(field_entry2%get_alias_name() == default_alias, is(true()))

      call field_entry2%set_alias_name(alias_name, rc=status)
      @assert_that(status, is(equal_to(0)))
      @assert_that(field_entry2%get_alias_name() == alias_name, is(true()))

      call field_entry2%set_alias_name(fail_name, rc=status)
      @assert_that(status, is(equal_to(1)))
      @assert_that(field_entry2%get_alias_name() == alias_name, is(true()))
   end subroutine test_set_alias_name

   @test
   subroutine test_name()
      type(test_FieldBundleEntry) :: field_entry_no_alias
      type(test_FieldBundleEntry) :: field_entry_alias

      @assert_that(field_entry_no_alias%call_standard_name, is(equal_to(0)))
      call field_entry_no_alias%initialize(short_name, component_name)
      @assert_that(field_entry_no_alias%name() == std_out_name, is(true()))
      @assert_that(field_entry_no_alias%call_standard_name, is(equal_to(1)))

      @assert_that(field_entry_alias%call_standard_name, is(equal_to(0)))
      call field_entry_alias%initialize(short_name, component_name, units=units, alias_name=alias_name)
      @assert_that(field_entry_alias%name() == alias_out_name, is(true()))
      @assert_that(field_entry_alias%call_standard_name, is(equal_to(1)))
   end subroutine test_name

   @test
   subroutine test_registry_entry()
      type(FieldBundleEntry)   :: field
      type(FieldRegistryEntry) :: field_entry

      call field%initialize(short_name, component_name, units=units, alias_name=alias_name)
      field_entry = field%registry_entry()
      @assert_that(field_entry%get_short_name()     == short_name,     is(true()))
      @assert_that(field_entry%get_component_name() == component_name, is(true()))
      @assert_that(field_entry%get_units()          == units,          is(true()))
   end subroutine test_registry_entry
end module Test_MAPL_FieldBundleEntry
